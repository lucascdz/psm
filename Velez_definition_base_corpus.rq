###############################################
# TITLE: Velez's Dictionary as Input: from definition to corpus through lexical bases
# DESCRIPTION: Searches for verbal lemmas linked to definitions that includes a given string ('fallar', i.e. 'to speak') and counts the frequency of verbal lemmas sharing the same lexical base occurring in a corpus of five historians' texts (Caesar, Sallustius, Tacitus, Quintus-Curtius and Hirtius) and two epic poems (Virgilius' Aeneid and Lucanus' Pharsalia).
###############################################

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX lila: <http://lila-erc.eu/ontologies/lila/>
PREFIX lime: <http://www.w3.org/ns/lemon/lime#>
PREFIX ontolex: <http://www.w3.org/ns/lemon/ontolex#>
PREFIX powla: <http://purl.org/powla/powla.owl#>


SELECT  ?lexicalbase ?label (count(?token) as ?occurrences) ?title
WHERE {
  #
  # filter the lexicon
  VALUES ?lexicon {
    <http://lila-erc.eu/data/lexicalResources/LatinPortuguese/Velez/Lexicon>
  } .
  ?lexicon lime:entry ?lexentry .
  ?lexentry ontolex:canonicalForm ?lemma ;
            ontolex:sense ?lexsense .
  ?lexsense <http://www.w3.org/2004/02/skos#definition> ?definition .
  FILTER (regex(?definition, "^fallar")||regex(?definition, " fallar"))
  #
  # prende i lemmi
  ?lemma lila:hasPOS lila:verb ;
         lila:hasBase ?lexicalbase .
  ?lexicalbase  rdfs:label ?label .
  #
  ?token lila:hasLemma ?lemmaT .
  ?lemmaT lila:hasPOS lila:verb ; lila:hasBase ?lexicalbase .
  ?texts ^powla:hasLayer ?token .
  ?texts dc:description ?title .
  VALUES ?texts {
    <http://lila-erc.eu/data/corpora/Lasla/id/corpus/CaesarBellum%20Gallicum/DocumentLayer>
    <http://lila-erc.eu/data/corpora/Lasla/id/corpus/CaesarBellum%20Civile/DocumentLayer>
    <http://lila-erc.eu/data/corpora/Lasla/id/corpus/CurtiusCurtius%20Historiae%20Alexandri%20Magni/DocumentLayer>
    <http://lila-erc.eu/data/corpora/Lasla/id/corpus/HirtiusBellum%20Gallicum/DocumentLayer>
    <http://lila-erc.eu/data/corpora/Lasla/id/corpus/SallustiusCatilina/DocumentLayer>
    <http://lila-erc.eu/data/corpora/Lasla/id/corpus/SallustiusJugurtha/DocumentLayer>
    <http://lila-erc.eu/data/corpora/Lasla/id/corpus/TacitusTac%20Annales/DocumentLayer>
    <http://lila-erc.eu/data/corpora/Lasla/id/corpus/TacitusTac%20Historiae/DocumentLayer>
    <http://lila-erc.eu/data/corpora/Lasla/id/corpus/VergiliusAeneis/DocumentLayer>
    <http://lila-erc.eu/data/corpora/CIRCSELatinLibrary/id/corpus/Pharsalia/DocumentLayer>
  }.
} order by ?label ?title